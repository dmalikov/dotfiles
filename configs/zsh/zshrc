autoload -U compinit promptinit colors

# completion settings
fpath=(~/git/zsh-completions/src $fpath)
compinit
zstyle ':completion::complete:*' use-cache 1

# vi mode
bindkey -v
export KEYTIMEOUT=1



# prompt settings
promptinit
colors
setopt transientrprompt
PROMPT=$'%F{3}%n@%M%f : %~ %(?,%F{2},%F{1})>> %f'
function zle-line-init zle-keymap-select {
    RPS1=""
    [[ ${IN_NIX_SHELL} -eq 1 ]] && RPS1+=" %F{8}[%F{3}nix-shelled%F{8}]%f "
    [[ -d ".cabal-sandbox" ]] && RPS1+=" %F{8}[%F{3}sandboxed%F{8}]%f "
    RPS1+="%F{2}${${KEYMAP/vicmd/NORMAL}/(main|viins)/}%f"
    RPS2=$RPS1
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

alias ssh='TERM=xterm-color ssh'
alias ihach='iconv -cf inis-cyrillic'
alias ls='ls --color=auto'
alias nix-shell='nix-shell --command zsh'

# external monitor helper
declare MAINDISPLAY=LVDS1
function setdisplay() {
    local -r NEWDISPLAY=${2:-DP2}
    case $1 in
            on)     xrandr --output $NEWDISPLAY --same-as $MAINDISPLAY --auto ;;
            off)    xrandr --output $NEWDISPLAY --off ;;
            *)      echo "Wrong argument"; return 1 ;;
    esac
}

function flac2mp3() {
    in=${1}
    out=${in:r}.mp3
    flac -d ${in} -c | lame - ${out} -b 320
}

# ruby stuff
unset RUBYOPT

# nixos stuff
if [[ -n "$(uname -a | grep -i -o "nixos")" ]]; then
    nixpkgs_head=/home/yep/git/nixpkgs/
    [[ -z "$(grep -o ${nixpkgs_head} <<< ${NIX_PATH})" ]] && export NIX_PATH=nixpkgs=${nixpkgs_head}:${NIX_PATH}
fi

bindkey '^R' history-incremental-search-backward
source ~/.zshenv

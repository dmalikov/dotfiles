autoload -U compinit promptinit colors

setopt autocd autopushd cdablevars correct interactivecomments noclobber sunkeyboardhack extendedglob

# completion settings
fpath=(~/git/zsh-completions/src $fpath)
compinit
zstyle ':completion::complete:*' use-cache 1

# vi mode
bindkey -v
export KEYTIMEOUT=1

# prompt settings
promptinit
colors
setopt transientrprompt

function prompt_hostname() {
    echo $'%F{3}%n@%M%f : %~'
}

function prompt_exit_code() {
    echo $'%(?,%F{2},%F{1})>>%f'
}

function prompt_static() {
    PROMPT="$(prompt_hostname) $(prompt_exit_code) "
}

function promt_dynamic() {
    PROMPT="$(prompt_hostname) "
    if [[ ${IN_NIX_SHELL} -eq 1 ]] then
        PROMPT+="%F{8}[%F{3}nix-shelled%F{8}]%f "
    fi
    if [[ -d ".cabal-sandbox" ]] then
        PROMPT+="%F{8}[%F{3}sandboxed%F{8}]%f "
    fi
    if [[ -n ${NIX_MYENV_NAME} ]] then
        PROMPT+="%F{8}[%F{6}${NIX_MYENV_NAME}%F{8}]%f "
    fi
    PROMPT+="$(prompt_exit_code) "
}

prompt_static
function zle-line-init zle-keymap-select {
    promt_dynamic
    RPS1=""
    RPS1+="%F{2}${${KEYMAP/vicmd/NORMAL}/(main|viins)/}%f"
    RPS2=$RPS1
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

source ~/git/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

alias ssh='TERM=xterm-color ssh'
alias ihach='iconv -cf inis-cyrillic'
alias ls='ls --color=auto'
alias ns="nix-env -qaP '*' | grep"
alias g='git'
alias t='tig'

# external monitor helper
declare MAINDISPLAY=LVDS1
function setdisplay() {
    local -r NEWDISPLAY=${2:-DP2}
    case $1 in
            on)     xrandr --output $NEWDISPLAY --same-as $MAINDISPLAY --auto ;;
            off)    xrandr --output $NEWDISPLAY --off ;;
            *)      echo "Wrong argument"; return 1 ;;
    esac
}

function flac2mp3() {
    in=${1}
    out=${in:r}.mp3
    flac -d ${in} -c | lame - ${out} -b 320
}

# ruby stuff
unset RUBYOPT

bindkey '^R' history-incremental-search-backward
source ~/.zshenv

nix_profile=~/.nix-profile/etc/profile.d/nix.sh
if [[ -e $nix_profile ]]; then
    source $nix_profile
fi
